plugins {
	id 'java'
	id 'org.springframework.boot' version '3.5.9'
	id 'io.spring.dependency-management' version '1.1.7'
	id 'jacoco'
	id 'org.asciidoctor.jvm.convert' version '3.3.2'
}

group = 'com.prism'
description = 'PR-ism 통계 서버'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
	asciidoctorExt
}

repositories {
	mavenCentral()
}

def queryDslVersion = '7.1'

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-validation'

	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation "io.github.openfeign.querydsl:querydsl-jpa:${queryDslVersion}"
	annotationProcessor "io.github.openfeign.querydsl:querydsl-apt:${queryDslVersion}:jpa"

	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'org.springframework.boot:spring-boot-starter-oauth2-client'

	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'

	runtimeOnly 'com.h2database:h2'
	runtimeOnly 'com.mysql:mysql-connector-j'

	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.security:spring-security-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

	asciidoctorExt 'org.springframework.restdocs:spring-restdocs-asciidoctor'
	testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc'
	testCompileOnly 'org.projectlombok:lombok'
	testAnnotationProcessor 'org.projectlombok:lombok'
}

tasks.named('test') {
	outputs.dir snippetsDir
	useJUnitPlatform()
	finalizedBy tasks.named('jacocoTestReport')
}

jacoco {
	toolVersion = "0.8.12"
}

def jacocoExcludePatterns = [
		"**/*Application*",
		"**/*Config*",
		"**/*Configuration*",

		"**/*Dto",
		"**/*Dto.*",
		"**/*DTO",
		"**/*DTO.*",
		"**/*Request",
		"**/*Request.*",
		"**/*Response",
		"**/*Response.*",
		"**/*View",
		"**/*View.*",
		"**/*Properties.*",
		"**/*PrivateClaims.*",
		"**/TokenScheme.*",

		"**/dto/**",
		"**/request/**",
		"**/requests/**",
		"**/response/**",
		"**/responses/**",
		"**/view/**",
		"**/views/**",
		"**/infrastructure/**",
		"**/global/**",
		"**/common/**",
		"**/exception/**",

		"**/Q*",
		"**/generated/**",
		"**/generated-src/**",
		"**/generated-sources/**",
		"**/properties/**"
]

tasks.named('jacocoTestReport') {
	dependsOn tasks.named('test')
	reports {
		html.required.set(true)
		xml.required.set(true)
		csv.required.set(false)
	}
	classDirectories.setFrom(
			files(classDirectories.files.collect { dir ->
				fileTree(dir: dir, exclude: jacocoExcludePatterns)
			})
	)
}

tasks.named('jacocoTestCoverageVerification') {
	dependsOn tasks.named('jacocoTestReport')
	classDirectories.setFrom(
			files(classDirectories.files.collect { dir ->
				fileTree(dir: dir, exclude: jacocoExcludePatterns)
			})
	)
	violationRules {
		rule {
			enabled = true
			element = 'METHOD'
			limit {
				counter = 'BRANCH'
				value = 'COVEREDRATIO'
				minimum = 0.50
			}
			limit {
				counter = 'LINE'
				value = 'COVEREDRATIO'
				minimum = 0.70
			}
		}
	}
}

tasks.register('jacocoTestCoverage') {
	group = 'verification'
	dependsOn tasks.named('test'),
			tasks.named('jacocoTestReport'),
			tasks.named('jacocoTestCoverageVerification')
}

tasks.named('check') {
	dependsOn tasks.named('jacocoTestCoverageVerification')
}

def querydslSrcDir  = layout.buildDirectory.dir("generated/querydsl").get().asFile

tasks.withType(JavaCompile).configureEach {
	options.getGeneratedSourceOutputDirectory().set(file(querydslSrcDir))
}

sourceSets {
	main {
		java {
			srcDirs += querydslSrcDir
		}
	}
}

ext {
	snippetsDir = file('build/generated-snippets')
}

asciidoctor {
	dependsOn test
	configurations 'asciidoctorExt'
	inputs.dir snippetsDir

	sources{
		include("**/index.adoc", "**/common/*.adoc")
	}

	baseDirFollowsSourceFile()
}

tasks.register('copyApiDocument', Copy) {
	dependsOn asciidoctor
	doFirst {
		delete file("src/main/resources/static/docs")
	}
	from asciidoctor.outputDir
	into file("src/main/resources/static/docs")
}

build {
	dependsOn copyApiDocument
}
